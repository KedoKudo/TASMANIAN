if (Tasmanian_ENABLE_SWIG)
  if (CMAKE_VERSION VERSION_LESS 3.18)
    list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/backport-cmake-318")
  endif()

  find_package(SWIG COMPONENTS fortran REQUIRED)

  # SWIG is requested and available; make sure it's the Fortran fork.
  cmake_policy(SET CMP0078 "NEW")
  cmake_policy(SET CMP0086 "NEW")
  include(UseSWIG)
endif()

set(Tasmanian_GENERATE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/generated")

function(tasmanian_add_swig_module name)
  if (Tasmanian_ENABLE_SWIG)
    # SWIG is available; actually generate the library dynamically.
    set(src_file "${CMAKE_CURRENT_SOURCE_DIR}/${name}.i")
    # We're using C++
    set_property(SOURCE "${src_file}" PROPERTY CPLUSPLUS ON)
    # We need to include the source directory
    set_property(SOURCE "${src_file}" PROPERTY USE_TARGET_INCLUDE_DIRECTORIES ON)
    # Use f03 extension
    set_property(SOURCE "${src_file}" PROPERTY COMPILE_OPTIONS -fext f03)

    # Create the library
    swig_add_library(${name}
      LANGUAGE Fortran
      TYPE USE_BUILD_SHARED_LIBS
      OUTPUT_DIR "${Tasmanian_GENERATE_DIR}"
      SOURCES "${src_file}" ${ARGN}
    )

  else()
    # SWIG is *not* being used: compile the code committed in the repository,
    # generated by the developer with SWIG.
    add_library(${name}
      "${Tasmanian_GENERATE_DIR}/${name}.f03"
      "${Tasmanian_GENERATE_DIR}/${name}FORTRAN_wrap.cxx"
      ${ARGN}
    )
  endif()
endfunction()

########################################################################
# Fortran librareis and command line tools
########################################################################

set(Tasmanian_libfor_target_name tasmanian)
tasmanian_add_swig_module(${Tasmanian_libfor_target_name})
target_link_libraries(${Tasmanian_libfor_target_name} Tasmanian_addons)
set_target_properties(${Tasmanian_libfor_target_name} PROPERTIES INSTALL_RPATH "${Tasmanian_final_install_path}/lib"
                                                                 SOVERSION ${Tasmanian_VERSION_MAJOR}
                                                                 VERSION   ${Tasmanian_version_string})

target_include_directories(${Tasmanian_libfor_target_name} PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/>)

if (Tasmanian_ENABLE_MPI)
    # see the comments in the Addons/CMakeLists.txt
    target_link_libraries(${Tasmanian_libfor_target_name} MPI::MPI_Fortran)
endif()

install(TARGETS ${Tasmanian_libfor_target_name}
        EXPORT "${Tasmanian_export_name}"
        RUNTIME DESTINATION "bin"
        LIBRARY DESTINATION "lib"
        ARCHIVE DESTINATION "lib")

unset(Tasmanian_libfor_target_name)

########################################################################
# Installation
########################################################################

install(FILES "${CMAKE_CURRENT_BINARY_DIR}/tasmanian.mod"
        DESTINATION include
        PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ WORLD_EXECUTE WORLD_READ)
